on slash command:
    event-string is "warn"
    
    set {_user} to argument "użytkownik" as user
    set {_reason} to argument "powód" as string ? "brak powodu"
    set {_id} to "%yaml value "bot" from "config"%"

    retrieve member with id discord id of {_user} from event-guild and store it in {_member}

#   ---  CHECKING IDIOTYZM  ------------------------------------------------------------------
    if {_member} is not set:
        reply with hidden styleDeny("Nie rozpoznano takiego użytkownika na serwerze.")
        stop
    
    if discord id of {_user} is discord id of event-user:
        reply with hidden styleDeny("Nie możesz zwarnować siebie.")
        stop
    
    if discord id of {_user} is {_id}:
        reply with hidden styleDeny("Nie mogę zwarnować sam siebie.")
        stop
    
    set {_role1::*} to roles of event-member
    set {_role2::*} to roles of {_member}

    if (role position of {_role2::1}) >= (role position of {_role1::1}):
        reply with hidden styleDeny("Nie możesz zwarnować użytkownika z tą samą lub wyższą rolą.")
        stop
#   ------------------------------------------------------------------------------------------

    defer the interaction and wait
    
    reply with penaltWarn(event-guild, {_user}, event-user, {_reason})
    
function penaltWarn(g: guild, u: user, a: user, r: string) :: embedbuilder:
    add 1 to {penalt::%discord id of {_g}%::warn::%discord id of {_u}%::%discord id of {_a}%}
    add 1 to {penalt::%discord id of {_g}%::warn::%discord id of {_u}%::all}

    set {_all} to {penalt::%discord id of {_g}%::warn::%discord id of {_u}%::all}
    set {_adm} to {penalt::%discord id of {_g}%::warn::%discord id of {_u}%::%discord id of {_a}%}

    make embed:
        set title of embed to "⚠️ Ostrzeżenie!"
        add inline field named "Użytkownik:" with value "%mention tag of {_u}%%nl%%{_u}%" to fields of embed
        add inline field named "Administrator:" with value "%mention tag of {_a}%%nl%%{_a}%" to fields of embed
        add inline field named "Ilość:" with value "`%{_all} ? 0%` wszystkich%nl%`%{_adm} ? 0%` administratora" to fields of embed
        add field named "Powód:" with value {_r} to fields of embed
        set embed color of embed to yellow
        # set thumbnail of embed to avatar of {_u}
        embedStart({_g})
        embedEnd()
    return last embed